# --- 1. Crear la VPC ---
resource "aws_vpc" "main" {
  # checkov:skip=CKV2_AWS_11: "Flow Logs desactivados por ahorro de costos en lab"
  # checkov:skip=CKV2_AWS_12: "Usamos Security Groups especificos, no el default"
  cidr_block           = var.vpc_cidr
  enable_dns_hostnames = true
  enable_dns_support   = true

  tags = {
    Name = "${var.project_name}-vpc"
  }
}

# --- 2. Internet Gateway ---
resource "aws_internet_gateway" "igw" {
  vpc_id = aws_vpc.main.id

  tags = {
    Name = "${var.project_name}-igw"
  }
}

# --- 3. Subredes Públicas ---
resource "aws_subnet" "public" {
  # checkov:skip=CKV_AWS_130: "Es una subred pública, requiere asignar IP pública para el ALB"
  count                   = length(var.public_subnets_cidr)
  vpc_id                  = aws_vpc.main.id
  cidr_block              = element(var.public_subnets_cidr, count.index)
  availability_zone       = element(var.availability_zones, count.index)
  map_public_ip_on_launch = true 

  tags = {
    Name = "${var.project_name}-public-subnet-${count.index + 1}"
  }
}

# --- 4. Subredes Privadas ---
resource "aws_subnet" "private" {
  count             = length(var.private_subnets_cidr)
  vpc_id            = aws_vpc.main.id
  cidr_block        = element(var.private_subnets_cidr, count.index)
  availability_zone = element(var.availability_zones, count.index)

  tags = {
    Name = "${var.project_name}-private-subnet-${count.index + 1}"
  }
}

# --- 5. Rutas ---
resource "aws_route_table" "public" {
  # checkov:skip=CKV2_AWS_44: "La ruta 0.0.0.0/0 es necesaria para el acceso a internet público"
  vpc_id = aws_vpc.main.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.igw.id
  }

  tags = {
    Name = "${var.project_name}-public-rt"
  }
}

resource "aws_route_table_association" "public" {
  count          = length(var.public_subnets_cidr)
  subnet_id      = element(aws_subnet.public.*.id, count.index)
  route_table_id = aws_route_table.public.id
}